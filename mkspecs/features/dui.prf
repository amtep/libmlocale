######################################################################
# DuiFeature
######################################################################

include(dui_defines.prf)
macx {
    INCLUDEPATH += /Library/Frameworks/dui.framework/Headers
    LIBS += -framework dui
} else {

    contains(TEMPLATE, app) {
        DEFINES += DUI_APPLICATION_NAME=\\\"${QMAKE_TARGET}\\\"
    } else {
        contains(TEMPLATE, lib) {
            DEFINES += DUI_LIBRARY_NAME=\\\"lib${QMAKE_TARGET}\\\"
        } else {
            # error(Unknown template)
        }
    }

    !contains(DEFINES, BUILD_DUI) {
        DUI_INC_DIR = $$DUI_PREFIX/include/dui
        DUI_LIB_DIR = $$DUI_LIBDIR

        GSTREAMER_INC = /usr/include/gstreamer-0.10 /usr/include/glib-2.0 /usr/lib/glib-2.0/include /usr/include/libxml2

        INCLUDEPATH += $${DUI_INC_DIR} $${GSTREAMER_INC}
        DEPENDPATH += $${DUI_INC_DIR}

        unix: QMAKE_LFLAGS += $${QMAKE_RPATH}$${DUI_LIB_DIR}
        LIBS += -L$${DUI_LIB_DIR} -lduicore -lduiviews -lduisettings -lduiextensions
        QMAKE_MOC = duimoc

        isEmpty(DUIGEN_OUTDIR) {
            DUIGEN_OUTDIR = .
        }

        duigenerator_model.name = duigenerator model
        duigenerator_model.input = MODEL_HEADERS
        duigenerator_model.output = $$DUIGEN_OUTDIR/gen_${QMAKE_FILE_BASE}data.cpp
        duigenerator_model.commands += duigen --model ${QMAKE_FILE_NAME} $$DUIGEN_OUTDIR
        duigenerator_model.clean += $$DUIGEN_OUTDIR/gen_*
        duigenerator_model.CONFIG = target_predeps no_link
        duigenerator_model.variable_out = GENERATED_SOURCES
        QMAKE_EXTRA_COMPILERS += duigenerator_model

        duigenerator_style.name = duigenerator style
        duigenerator_style.input = STYLE_HEADERS
        duigenerator_style.output = $$DUIGEN_OUTDIR/gen_${QMAKE_FILE_BASE}data.cpp
        duigenerator_style.commands += duigen --style ${QMAKE_FILE_NAME} $$DUIGEN_OUTDIR
        duigenerator_style.clean += $$DUIGEN_OUTDIR/gen_*
        duigenerator_style.CONFIG = target_predeps no_link
        duigenerator_style.variable_out = GENERATED_SOURCES
        QMAKE_EXTRA_COMPILERS += duigenerator_style
    }
}
