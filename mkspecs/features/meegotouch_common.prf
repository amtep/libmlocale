include(meegotouch_defines.prf)
include(meegotouch_mgen.prf)

macx {
    INCLUDEPATH += /Library/Frameworks/m.framework/Headers
}

contains(TEMPLATE, app) {
    DEFINES += M_APPLICATION_NAME=\\\"${QMAKE_TARGET}\\\"
} else {
    contains(TEMPLATE, lib) {
        DEFINES += M_LIBRARY_NAME=\\\"lib${QMAKE_TARGET}\\\"
    } else {
        # error(Unknown template)
    }
}

!contains(DEFINES, BUILD_M) {
    M_INC_DIR = $$M_PREFIX/include/meegotouch
    M_LIB_DIR = $$M_LIBDIR

    GSTREAMER_INC = /usr/include/gstreamer-0.10 /usr/include/glib-2.0 /usr/lib/glib-2.0/include /usr/include/libxml2

    INCLUDEPATH += $${M_INC_DIR} $${GSTREAMER_INC}
    DEPENDPATH += $${M_INC_DIR}

    unix: QMAKE_LFLAGS += $${QMAKE_LFLAGS_RPATH}$${M_LIB_DIR}

    LIBS += -L$${M_LIB_DIR}

    QMAKE_MOC = $${M_MMOC_EXECUTABLE}
}
